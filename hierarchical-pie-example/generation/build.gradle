/**
 * $ ./gradlew
 * The default task will build the project and run the test suite inside
 * your local spark environment (spark-submit must be on the PATH).
 *
 * A coverage report will be present at build/reports/scoverage/index.html
 *
 * TODO integrate https://github.com/kt3k/coveralls-gradle-plugin
 */

apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'maven'
apply plugin: 'idea'

group = 'software.uncharted.salt'
version = '3.0.0'

project.ext {
  scalaBinaryVersion = '2.10'
  scalaVersion = '2.10.4'
  sparkVersion = '1.6.1'
  saltVersion = '3.0.0'
}

jar {
  baseName = 'salt-hierarchical-pie-example'
  version =  version
  dependsOn configurations.runtime
  from {
    (configurations.runtime - configurations.provided).collect {
      it.isDirectory() ? it : zipTree(it)
    }
  } {
    exclude "META-INF/*.SF"
    exclude "META-INF/*.DSA"
    exclude "META-INF/*.RSA"
  }
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    provided
    compile.extendsFrom provided
}

dependencies {
    //salt
    compile "software.uncharted.salt:salt-core:${saltVersion}"

    //uncharted spark pipeline
    compile "software.uncharted.sparkpipe:sparkpipe-core:0.9.7"

    //sqlite
    compile 'org.xerial:sqlite-jdbc:3.8.11.2'

    //scala
    provided("org.scala-lang:scala-library:${scalaVersion}")

    //spark
    provided "org.apache.spark:spark-core_${scalaBinaryVersion}:${sparkVersion}"
    provided "org.apache.spark:spark-sql_${scalaBinaryVersion}:${sparkVersion}"
    provided "org.scala-lang:scala-library:$scalaBinaryVersion"

    compile "com.databricks:spark-csv_2.10:1.2.0"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

task run(overwrite: true, type: Exec, dependsOn: [assemble]) {
  executable = 'spark-submit'
  args = ["--class","software.uncharted.salt.examples.hpie.Main","/opt/salt/build/libs/salt-hierarchical-pie-example-${version}.jar", "/opt/data/ubuntu-filelist.csv", "/opt/output"]
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = file("$buildDir/classes/main/")
    }
}

defaultTasks 'run'
